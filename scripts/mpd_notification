#!/bin/zsh

summary="$2"
body="$3"
urgency="$5"

dunst_pics="$(readlink -f ${HOME}/tmp/dunst-images)"
cover_lock="${dunst_pics}/notify.lock"
image="/tmp/.mpd-notification-artwork.png"

function wf(){
    print "${col1}⟬${end}${bold}$1${end}${col1}⟭${end} "
}

function get_pid(){
    local mpdsc="$(which mpdscribble) --conf"
    local mpdas="${XDG_CONFIG_HOME}/mpdas"
    pgrep -f "${mpdsc} ${XDG_CONFIG_HOME}/mpdscribble/${1}.conf --no-daemon" || \
    pgrep -f "${mpdas}/${1}.rc"
}

function get_lastfm(){
    for pid in neg hextrick; do
        if [[ $(get_pid "${pid}") != "" ]]; then
            printf "%s\n" "$(wf "L")$(sed 's:^.:\U&:g' <<< ${pid}) last.fm"
            break
        fi
    done
}

function highlight(){
    local msg="$(xrq color${1})"
    print "<span weight='bold' color='${msg}'>"
}

function notify(){
    local col1=$(highlight 4)
    local col2=$(highlight 8)

    local end="</span>"
    local bold="<span weight='bold'>"
    local -a bodyarr
    while read line; do
        bodyarr+="${line}"
    done <<< ${body}

    local -a len=()
    for i in {1..6}; len+=${#bodyarr[$i]};
    len=($(printf "%s\n" "${len[@]}"|sort -nr))

    local max_len=35
    len=$[${(@)len[1]}]
    [[ ${len} > ${max_len} ]] && len=${max_len}

    local -a fancy_table=('' '♫' '⟫' '')

    for i in {1..$#fancy_table}; do
        if [[ ${bodyarr[i]} != "" ]]; then
            bodyarr[i]=$(wf ${fancy_table[i]})"${bodyarr[i]}\n"
        else
            bodyarr[i]="\n"
        fi
    done

    local current_file="$(mpc -f '%file%'|head -1)"
    local mpd_music_dir="$(awk '/music_directory/{print $2}' /etc/mpd.conf|tr -d '"')"
    local current_dirname="${mpd_music_dir}/$(dirname "${current_file}")"

    albumdetails_metadata="$(find "${current_dirname}" -exec albumdetails '{}' + 2>&1 >/dev/null \
        | awk -F ': ' '/Quality/{print $2}' \
        | sed -e 's; / ;·;g' -e 's; channels;;')"
    sox_metadata="$(sox --i "${mpd_music_dir}/${current_file}" 2> /dev/null)"
    if [[ ! -z "${sox_metadata// }" ]]; then
        media_file_encoding="$(awk -F': ' '/Sample Encoding/{print $2}' <<< "${sox_metadata}")"
        case "${media_file_encoding}" in
            *FLAC*) file_enc="flac" ;;
            *MPEG*) file_enc="mp3" ;;
        esac
        file_enc="${file_enc}:$(awk -F': ' '/Precision/{print $2}' <<< ${sox_metadata})"
    fi

    [[ ${file_enc// } != "" ]] && \
        bodyarr[5]=$(wf '')"${file_enc}\n" || \
        bodyarr[5]="\n"
    [[ ${albumdetails_metadata// } != "" ]] && \
        bodyarr[6]=$(wf '')"${albumdetails_metadata}\n" || \
        bodyarr[6]="\n"

    local output
    for i in ${bodyarr[@]};
        [[ ${i} != "\n" ]] && output="${output}${i}"

    summary="${col2}$(repeat ${len} print -n "◇")${end}"
    output="${output}$(get_lastfm)\n${summary}"

    summary=$(echo "${summary}")
    output=$(echo "${output}")

    dunstify -p -a "mpd_notification" -h "string:fgcolor:#8DA6C4" -u "${urgency}" -i "${image}" "${summary}" "${output}" > /tmp/notify_id
    echo "${body}$(get_lastfm)" > "${cover_lock}"
}

function cmp_img(){
    ${HOME}/bin/scripts/compare_images.py "${1}" "${2}" \
        || print "False"
}

function show_notify(){
    if [[ ! -f "${cover_lock}" ]]; then
        notify
    elif [[ -s "${cover_lock}" ]]; then
        if [[ "$(< "${cover_lock}")" != "${body}$(get_lastfm)" ]]; then
            notify
        fi
    fi
    cp "${image}" "${prev}"
}

function zshexit(){
    (
        hashsum1=$(xxh32sum "${image}"|cut -d ' ' -f1)
        sleep 4s
        hashsum2=$(xxh32sum "${image}"|cut -d ' ' -f1)
        [[ ${hashsum1} = ${hashsum2} ]] && rm -f ${image}
    ) &
}

function main(){
    dunstify -C "$(< /tmp/notify_id)"

    [[ ! -d "${dunst_pics}" ]] && mkdir -p "${dunst_pics}"
    prev="${dunst_pics}/prev.png"

    find "${dunst_pics}" -name prev.png -not -newermt '-900 seconds' -delete
    if [[ -f ${image} ]]; then
        [[ ! -d "${dunst_pics}" ]] && mkdir -p "${dunst_pics}"

        # send notification with compatible image
        if [[ ! -f "${prev}" ]]; then
            show_notify
        else
            local dunst_picslist=$(setopt nullglob dotglob; print "${dunst_pics}"/*)
            if [[ $(cmp_img "${image}" "${prev}") == "False" ]] \
                && (( ${#dunst_picslist[@]} > 1 )); then
                show_notify
            fi
        fi
    fi
}

main "$@"
