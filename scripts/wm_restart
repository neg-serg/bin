#!/bin/zsh

wait_a_bit(){ sleep .2s; }
soft_restart(){ [[ ${windowmanager} == "notion" ]] && restart_panel }

function restart_panel() {
    for i in admiral rofi dzen2; pkill "${i}"
    ${SCRIPT_HOME}/panels
}

function notion_restart() {
    restart_panel
    if [[ ${1} != "soft" ]]; then
        notionflux -e 'notioncore.restart()'
    fi
}

function windowchef_restart(){
    "${XDG_CONFIG_HOME}/windowchef/windowchefrc"
    pkill -USR1 -x sxhkd
}

function i3::common_restart(){
    ps -ef | awk -v name="bar" '$0 ~ name {print $2}' | xargs kill 2>/dev/null
    polybar -q -c ${polybar_cfg} tiny &
    while ! pgrep -f "polybar -q -c ${XDG_CONFIG_HOME}/polybar/main tiny"; do
        wait_a_bit
    done && sleep 1s && polybar -q -c ${polybar_cfg} main
    ppi3 ${XDG_CONFIG_HOME}/i3/_config > ${XDG_CONFIG_HOME}/i3/config
}

function i3_restart(){
    polybar_cfg=${XDG_CONFIG_HOME}/polybar/main
    # kill any current poppers (thanks Dylan)
    if [[  $(i3 --version|awk '{print $3}'|grep '^4.13' -ow) == "4.13" ]]; then
        i3::common_restart
        wait_a_bit
        notify-send "[${wm_}] reload"
        i3-msg reload
        ps aux \
            | grep python \
            | grep i3 \
            | awk '{print $2}' \
            | xargs kill
        i3-msg "exec --no-startup-id python3 ${XDG_CONFIG_HOME}/i3/disable-standby-fs.py"
        i3-msg "exec --no-startup-id python3 ${XDG_CONFIG_HOME}/i3/nsd.py"
        i3-msg "exec --no-startup-id python3 ${XDG_CONFIG_HOME}/i3/circled.py"
        i3-msg "exec --no-startup-id python3 ${XDG_CONFIG_HOME}/i3/flastd.py"
    else
        i3::common_restart
        wait_a_bit
        notify-send "[${wm_}] restart"
        i3-msg restart
    fi
}

function main() {
    zmodload -aF zsh/system b:zsystem
    local lockfile="${HOME}/tmp/$(basename $0).lock"
    local timeout=2

    if [[ -f ${lockfile} ]]; then
        exit 1
    fi

    touch "${lockfile}"
    (
        if zsystem flock -t "${timeout}" -r "${lockfile}"; then
            echo "Locking to [${lockfile}] succeeded" >&2

            #----------------------------------------------
            if [[ ${1} != "" ]]; then
                if [[ ${1} == "soft" ]]; then
                    soft_restart
                else
                    wm_="$1"
                    shift
                    eval "$(echo ${wm_}_restart) $@"
                fi
            else
                wm_="${windowmanager}"
                eval "$(echo ${wm_}_restart)"
            fi
            #----------------------------------------------
        fi
        sleep ${timeout}s
        rm -v "${lockfile}"
    ) &
    
}

main "$@"
